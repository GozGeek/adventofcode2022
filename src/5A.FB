. SET UP DEFAULTS
MH=8:. INITIAL MAX HEIGHT
NS=9:. NUMBER OF STACKS
FILE$="D:5DATA.TXT":. FILE OF DATA

. ARRAY FOR EACH STACK
DIM S1(100) BYTE,S2(100) BYTE
DIM S3(100) BYTE,S4(100) BYTE
DIM S5(100) BYTE,S6(100) BYTE
DIM S7(100) BYTE,S8(100) BYTE
DIM S9(100) BYTE

DIM S(9)
S(1)=&S1:S(2)=&S2:S(3)=&S3:S(4)=&S4
S(5)=&S5:S(6)=&S6:S(7)=&S7:S(8)=&S8:S(9)=&S9

. LOAD DATA INTO ARRAYS
OPEN #1,4,0,FILE$
ROW=MH
INPUT #1,I$
WHILE I$[2,1]<>"1"
 STACK=1
 FOR X=2 TO NS*4-2 STEP 4
  IF I$[X,1]<>" "
   CRATE=ASC(I$[X,1])
   IF PEEK(S(STACK))<ROW THEN POKE S(STACK),ROW
   POKE S(STACK)+ROW,CRATE
  ENDIF
  INC STACK
 NEXT X
 DEC ROW
 INPUT #1,I$
WEND

@PRINTSTACKS

.BURN INPUT
INPUT #1,I$

INPUT #1,I$
REPEAT
 .? I$
 IF I$[7,1]=" "
  NUMSTEPS=VAL(I$[6,1])
 ELSE
  NUMSTEPS=VAL(I$[6,2])
 ENDIF

 IF NUMSTEPS<10
  SS=VAL(I$[13,1])
  DS=VAL(I$[18,1])
 ELSE
  SS=VAL(I$[14,1])
  DS=VAL(I$[19,1])
 ENDIF

 ? "MOVE:";STR$(NUMSTEPS);" SS:";STR$(SS);" DS:";STR$(DS)

 .MOVE CRATES
 FOR X=1 TO NUMSTEPS
  CRATE=PEEK(S(SS)+PEEK(S(SS)))
  POKE S(DS),PEEK(S(DS))+1
  POKE S(DS)+PEEK(S(DS)),CRATE

  POKE S(SS)+PEEK(S(SS)),32
  POKE S(SS),PEEK(S(SS))-1

 NEXT X

 .NEXT ROW
 INPUT #1,I$
UNTIL I$=""

@PRINTSTACKS

. PROCEDURES

PROC PRINTSTACKS
 MAX=0
 FOR X=1 TO NS
  IF PEEK(S(X))> MAX THEN MAX=PEEK(S(X))
 NEXT X

 FOR X=MAX TO 1 STEP -1
  FOR Y=1 TO NS
   ? "[";CHR$(PEEK(S(Y)+X));"] ";
  NEXT Y
  ?
 NEXT X

 FOR X=1 TO NS
  ? " ";STR$(X);"  ";
 NEXT X
 ?
ENDPROC

